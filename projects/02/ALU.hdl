// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs,
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
        // Negating X, for input if nx=1
        Not16(in=x,out=negX);
        // Muxing the selections of zx & nx
        Mux4Way16(a=x,b=false,c=negX,d=true,sel[0]=zx,sel[1]=nx,out=muxedX);

        // Negating Y, for input if ny=1
        Not16(in=y,out=negY);
        // Muxing the selections of zx & nx
        Mux4Way16(a=y,b=false,c=negY,d=true,sel[0]=zy,sel[1]=ny,out=muxedY);

        Add16(a=muxedX,b=muxedY,out=addedXandY);
        And16(a=muxedX,b=muxedY,out=andedXandY);
        Mux16(a=andedXandY,b=addedXandY,sel=f,out=fXandY);

        Not16(in=fXandY,out=notfXandY);

        Mux16(a=fXandY,b=notfXandY,sel=no,out[15]=ng,out[0..7]=out1,out[8..15]=out2,out=out);

        Or8Way(in=out1,out=zr1);
        Or8Way(in=out2,out=zr2);
        Or(a=zr1,b=zr2,out=notzr);
        Not(in=notzr,out=zr);
}
